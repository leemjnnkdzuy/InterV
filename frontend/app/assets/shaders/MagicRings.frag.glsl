precision highp float;

uniform float uTime, uAttenuation, uLineThickness;
uniform float uBaseRadius, uRadiusStep, uScaleRate;
uniform float uOpacity, uNoiseAmount, uRotation, uRingGap;
uniform float uFadeIn, uFadeOut;
uniform float uMouseInfluence, uHoverAmount, uHoverScale, uParallax, uBurst;
uniform float uHorizontalScale;
uniform float uCenterGapWidth, uCenterGapHeight, uCenterGapSoftness;
uniform vec2 uResolution, uMouse;
uniform vec3 uColor, uColorTwo;
uniform int uRingCount;

const float HP = 1.5707963;
const float CYCLE = 3.45;

float fade(float t) {
  return t < uFadeIn ? smoothstep(0.0, uFadeIn, t) : 1.0 - smoothstep(uFadeOut, CYCLE - 0.2, t);
}

float ring(vec2 p, float ri, float cut, float t0, float px) {
  float t = mod(uTime + t0, CYCLE);
  float r = ri + t / CYCLE * uScaleRate;
  float d = abs(length(p) - r);
  float a = atan(abs(p.y), abs(p.x)) / HP;
  float th = max(1.0 - a, 0.5) * px * uLineThickness;
  float h = (1.0 - smoothstep(th, th * 1.5, d)) + 1.0;
  d += pow(cut * a, 3.0) * r;
  return h * exp(-uAttenuation * d) * fade(t);
}

void main() {
  float px = 1.0 / min(uResolution.x, uResolution.y);
  vec2 p = (gl_FragCoord.xy - 0.5 * uResolution.xy) * px;
  float cr = cos(uRotation), sr = sin(uRotation);
  p = mat2(cr, -sr, sr, cr) * p;
  p -= uMouse * uMouseInfluence;
  float sc = mix(1.0, uHoverScale, uHoverAmount) + uBurst * 0.3;
  p /= sc;
  float horizontalScale = max(uHorizontalScale, 0.01);
  vec2 ringP = vec2(p.x / horizontalScale, p.y);
  vec2 ringMouse = vec2(uMouse.x / horizontalScale, uMouse.y);
  vec3 c = vec3(0.0);
  float alphaMask = 0.0;
  float rcf = max(float(uRingCount) - 1.0, 1.0);
  for (int i = 0; i < 10; i++) {
    if (i >= uRingCount) break;
    float fi = float(i);
    vec2 pr = ringP - fi * uParallax * ringMouse;
    vec3 rc = mix(uColor, uColorTwo, fi / rcf);
    float ringAlpha = ring(pr, uBaseRadius + fi * uRadiusStep, pow(uRingGap, fi), i == 0 ? 0.0 : 2.95 * fi, px);
    c = mix(c, rc, vec3(ringAlpha));
    alphaMask = max(alphaMask, ringAlpha);
  }
  c *= 1.0 + uBurst * 2.0;
  float n = fract(sin(dot(gl_FragCoord.xy + uTime * 100.0, vec2(12.9898, 78.233))) * 43758.5453);
  c += (n - 0.5) * uNoiseAmount;
  c = max(c, vec3(0.0));
  vec2 edgeUv = abs((gl_FragCoord.xy / uResolution.xy) - 0.5) * 2.0;
  float edgeFade = 1.0 - smoothstep(0.92, 1.0, max(edgeUv.x, edgeUv.y));
  float centerGap = 0.0;
  if (uCenterGapWidth > 0.0 && uCenterGapHeight > 0.0) {
    float gapX = 1.0 - smoothstep(uCenterGapWidth, uCenterGapWidth + uCenterGapSoftness, edgeUv.x);
    float gapY = 1.0 - smoothstep(uCenterGapHeight, uCenterGapHeight + uCenterGapSoftness, edgeUv.y);
    centerGap = gapX * gapY;
  }
  float alpha = smoothstep(0.02, 0.28, alphaMask) * edgeFade * uOpacity;
  alpha *= 1.0 - centerGap;
  gl_FragColor = vec4(c, alpha);
}
